option (ROBOARD
    "Compile for Roboard" OFF)

if (ROBOARD)
    set (CMAKE_CXX_FLAGS "-m32 -mtune=i586 -march=i586"
        CACHE STRING "")
    set (CMAKE_CXX_COMPILER "g++-4.4"
        CACHE STRING "")
endif (ROBOARD)

cmake_minimum_required (VERSION 2.6)
project (RhobanUtils)
find_package (Threads)

option (HAVE_COLORS 
    "Have terminal colors" ON)

if (HAVE_COLORS)
    add_definitions (-DHAVE_COLORS)
endif (HAVE_COLORS)

option (COMPILE_ZMQ
    "Do we need to compile ZMQ" OFF)

option (HAVE_ZMQ
    "Do we have ZMQ" OFF)

if (HAVE_ZMQ)
    add_definitions (-DHAVE_ZMQ)
    set(ZMQ_HEADERS "${BASE}/Utils/zmq/include"
        CACHE STRING "ZMQ headers directory")
endif (HAVE_ZMQ)

IF (${CMAKE_SYSTEM_NAME} MATCHES "Darwin")
   # Mac OS X specific code
   SET(SYSTEM_OS "MACOSX")
   add_definitions(-DMACOSX)
   MESSAGE ("-- Running on Mac OS X platform")
ENDIF (${CMAKE_SYSTEM_NAME} MATCHES "Darwin")

IF(${CMAKE_SYSTEM_NAME} MATCHES "Linux")
    # Linux specific code
   SET(SYSTEM_OS "LINUX")
   add_definitions(-DLINUX)
   MESSAGE ("-- Running on Linux platform")
ENDIF(${CMAKE_SYSTEM_NAME} MATCHES "Linux")

IF(DEFINED WIN32)
    # Windows specific code
    SET(SYSTEM_OS "WINDOWS")
    add_definitions(-DWINDOWS)
    MESSAGE ("-- Running on Windows platform")
ENDIF(DEFINED WIN32)

# Options
option (RHOBANMATH
    "Enables RhobanMath support" OFF)

set (RHOBAN_UTILS_DIRECTORY "${PROJECT_SOURCE_DIR}")

if (NOT RHOBANMATH)
    add_definitions(-DNO_RHOBANMATH)
endif (NOT RHOBANMATH)

if (RHOBANMATH)
    set(RHOBAN_MATH_DIRECTORY "${BASE}/Math"
        CACHE STRING "RhobanMath directory")

    include_directories ("${RHOBAN_MATH_DIRECTORY}")
    set (EXTRA_LIBS ${EXTRA_LIBS} RhobanMath)
endif (RHOBANMATH)

# Adding sockets for Win32
if (WIN32)
    set (EXTRA_LIBS ${EXTRA_LIBS} ws2_32)
endif (WIN32)

message("-- Adding YAML support")
option(YAML_CPP_BUILD_TOOLS "Enable testing and parse tools" OFF)
add_subdirectory ("${PROJECT_SOURCE_DIR}/yaml-cpp")
set (YAML_HEADERS "${PROJECT_SOURCE_DIR}/yaml-cpp/include"
    CACHE STRING "")
include_directories ("${YAML_HEADERS}")
set (EXTRA_LIBS ${EXTRA_LIBS} yaml-cpp)

message("-- Adding JSON support")
add_subdirectory ("${PROJECT_SOURCE_DIR}/jsoncpp")
set (JSON_HEADERS "${PROJECT_SOURCE_DIR}/jsoncpp/include"
    CACHE STRING "")
include_directories ("${JSON_HEADERS}")
set (EXTRA_LIBS ${EXTRA_LIBS} json)

# Sources
set(DIRECTORIES
    .
    configfile
    graph
    json
    logging
    observer
    serial
    sockets
    tests
    threading
    timing
    xml
    )

if (COMPILE_ZMQ)
    add_subdirectory(zmq)
    include_directories ("${ZMQ_HEADERS}")
    set (EXTRA_LIBS ${EXTRA_LIBS} ZeroMQ)

    if(UNIX AND NOT APPLE)
        set (EXTRA_LIBS ${EXTRA_LIBS} rt)
    endif(UNIX AND NOT APPLE)
endif (COMPILE_ZMQ)

if(HAVE_ZMQ)
    # include_directories ("${ZMQ_HEADERS}")
    if (WIN32)
        set (ZMQ_LIB_DIR "${PROJECT_SOURCE_DIR}/zmq/builds/win32"
        CACHE STRING "Directory where to find the zmq lib")
    	link_directories ("${ZMQ_LIB_DIR}")
	endif (WIN32)
endif(HAVE_ZMQ)

foreach (DIRECTORY ${DIRECTORIES})
    message("-- Utils: Adding ${DIRECTORY}/")
    include_directories("${CMAKE_CURRENT_SOURCE_DIR}/${DIRECTORY}")
    include (${DIRECTORY}/Sources.cmake)

    set (PREFIXED_SOURCES)
    foreach (SOURCE ${SOURCES})
        set (PREFIXED_SOURCES ${PREFIXED_SOURCES} ${DIRECTORY}/${SOURCE})
    endforeach (SOURCE)

    set (ALL_SOURCES ${ALL_SOURCES} ${PREFIXED_SOURCES})
endforeach (DIRECTORY)

# Adding TinyXml
add_subdirectory (xml/TinyXml)
set (EXTRA_LIBS ${EXTRA_LIBS} TinyXml ${CMAKE_THREAD_LIBS_INIT})

# Adding library and dependecy
add_library (RhobanUtils ${ALL_SOURCES})
target_link_libraries (RhobanUtils ${EXTRA_LIBS})
